from snakemake.io import glob_wildcards, expand

# grab user settings and define variables
configfile: "../config/config.yaml"
workdir: config["outdir"]
Q2CONDA = config["q2env"]
PRIMERS = config["primers"]
FORMAT = ["seqs", "tax"]

# Variables below, allow us to contsrain "amplicon extraction"
# to only those file names with variable regions as defined
# in PRIMERS, i.e. ignore '-fl-'
VREGION = list(PRIMERS.keys())
VREGION_CONSTRAINT = "|".join(VREGION)


rule all:
    input:
        expand("{vregion}/silva-{vregion}-classifier.qza", 
                vregion=VREGION),
        "fl/silva-fl-derep-classifier.qza",
        "fl/silva-fl-cull-classifier.qza",
        "fl/silva-filt-classifier.qza",

include:
    "rules/common.smk"

#############
# Get silva #
#############
## get silva data
rule get_silva:
    conda: 
        Q2CONDA
    params:
        version = config["silva_opt"]["version"],
        target = config["silva_opt"]["target"],
        ranks = config["silva_opt"]["ranks"],
    output:
        seqs = "fl/silva-fl-rnaseqs.qza",
	    tax = "fl/silva-fl-tax.qza"
    log:
        out = "logs/downloads-log.out",
        err = "logs/downloads-log.err"
    shell: 
        """
        qiime rescript get-silva-data \
            --p-version {params.version} \
            --p-target {params.target} \
            --p-ranks {params.ranks} \
            --p-rank-propagation \
            --o-silva-sequences {output.seqs} \
            --o-silva-taxonomy {output.tax} \
            --verbose 2> {log.err} 1> {log.out}
        """

rule reverse_transcribe:
    conda:
         Q2CONDA
    input:
        rna_seqs = rules.get_silva.output.seqs
    output:
        seqs = "fl/silva-fl-seqs.qza"
    log:
        out = "logs/reverse-transcribe-log.out",
        err = "logs/reverse-transcribe-log.err"
    shell:
        """
        qiime rescript reverse-transcribe \
            --i-rna-sequences {input.rna_seqs} \
            --o-dna-sequences {output.seqs} \
            --verbose 2> {log.err} 1> {log.out}
        """


# derep
rule full_length_derep:
    conda: 
        Q2CONDA
    input:
        seqs = rules.reverse_transcribe.output.seqs,
        tax = rules.get_silva.output.tax
    params:
        mode = config["derep"]["mode"],
    threads:
        config["threads"]["general"]
    output:
        derep_seqs = "fl/silva-fl-derep-seqs.qza",
	    derep_tax = "fl/silva-fl-derep-tax.qza"
    log:
        out = "logs/derep-fl-log.out",
        err = "logs/derep-fl-log.err"
    shell:
        """
        qiime rescript dereplicate \
            --i-sequences {input.seqs} \
            --i-taxa {input.tax} \
            --p-mode {params.mode} \
	        --p-threads {threads} \
            --o-dereplicated-sequences {output.derep_seqs} \
            --o-dereplicated-taxa {output.derep_tax}
        """

#################################
# remove poor quality sequences #
#################################
rule cull_seqs:
    conda: 
        Q2CONDA
    input:
        seqs = "{ref_type}/silva-{ref_type}-derep-seqs.qza",
    output:
        seqs = "{ref_type}/silva-{ref_type}-cull-seqs.qza"
    params:
        ndegen = config["cull_opt"]["ndegen"],
        hpoly = config["cull_opt"]["hpoly"]
    threads:
        config["threads"]["general"]
    log:
        out = "logs/cull-seqs-{ref_type}-log.out",
        err = "logs/cull-seqs-{ref_type}-log.err"
    shell:
        """
        qiime rescript cull-seqs \
            --i-sequences {input.seqs} \
            --p-num-degenerates {params.ndegen} \
            --p-homopolymer-length {params.hpoly} \
            --p-n-jobs {threads} \
            --o-clean-sequences {output.seqs} \
            --verbose 2> {log.err} 1> {log.out}
        """



################################################
# Generate Several Variable Region Classifiers #
################################################
rule extract_amplicon_region:
    wildcard_constraints:
        vregion = VREGION_CONSTRAINT
    conda: 
        Q2CONDA 
    input:
        seqs = rules.full_length_derep.output.derep_seqs,
    params:
        f_primer = get_fw_primer,
        r_primer = get_rev_primer,
        orientation = "forward"
    threads: 
        config["threads"]["general"]
    output:
        extracted_seqs = "{vregion}/silva-{vregion}-seqs.qza"
    log:
        out = "logs/extract-amp-{vregion}-log.out",
        err = "logs/extract-amp-{vregion}-log.err"
    shell:
        """
        qiime feature-classifier extract-reads \
            --i-sequences {input.seqs} \
	        --p-f-primer {params.f_primer} \
	        --p-r-primer {params.r_primer} \
	        --p-n-jobs {threads} \
	        --p-read-orientation {params.orientation} \
	        --o-reads {output.extracted_seqs} \
            --verbose 2> {log.err} 1> {log.out}
        """

# variable region derep
rule variablbe_region_derep:
    wildcard_constraints:
        vregion = VREGION_CONSTRAINT
    conda: 
        Q2CONDA
    input:
        seqs = rules.extract_amplicon_region.output.extracted_seqs,
        tax = rules.full_length_derep.output.derep_tax
    params:
        mode = config["derep"]["mode"],
    threads:
        config["threads"]["general"]
    output:
        derep_seqs = "{vregion}/silva-{vregion}-derep-seqs.qza",
	    derep_tax = "{vregion}/silva-{vregion}-derep-tax.qza"
    log:
        out = "logs/derep-{vregion}-log.out",
        err = "logs/derep-{vregion}-log.err"
    shell:
        """
        qiime rescript dereplicate \
            --i-sequences {input.seqs} \
            --i-taxa {input.tax} \
            --p-mode {params.mode} \
	        --p-threads {threads} \
            --o-dereplicated-sequences {output.derep_seqs} \
            --o-dereplicated-taxa {output.derep_tax}
        """

# train plain full-length and variable region classifiers,
# e.g. 'v1v3', 'V4', etc... 
rule vr_train:
    wildcard_constraints:
        vregion = VREGION_CONSTRAINT
    conda:
        Q2CONDA
    input:
        seqs = "{vregion}/silva-{vregion}-cull-seqs.qza",
	    tax = "{vregion}/silva-{vregion}-derep-tax.qza"
    output:
        classifier = "{vregion}/silva-{vregion}-classifier.qza"
    log:
        out = "logs/train-classifier-{vregion}-log.out",
        err = "logs/train-classifier-{vregion}-log.err"
    shell:
        """
	    qiime feature-classifier fit-classifier-naive-bayes \
            --i-reference-reads {input.seqs} \
            --i-reference-taxonomy {input.tax} \
            --o-classifier {output.classifier} \
            --verbose 2> {log.err} 1> {log.out}
	    """

##############################################################
# Filter by length and taxon for full length SILVA seqs only #
##############################################################
rule full_length_filter_seq_by_len_and_tax:
    conda:
        Q2CONDA
    input:
        seqs = "fl/silva-fl-cull-seqs.qza",
	    tax =  "fl/silva-fl-derep-tax.qza"
    params:
        labels = config["filt_seq_len_opt"]["labels"],
        min_lens = config["filt_seq_len_opt"]["min_lens"]
    threads:
        config["threads"]["general"]
    output:
        filt_seqs = "fl/silva-filt-seqs.qza",
	    disc_seqs = "fl/silva-disc-seqs.qza"
    log:
        out = "logs/silva-seq-filt-len-tax-log.out",
        err = "logs/silva-seq-filt-len-tax-log.err"
    shell:
        """
        qiime rescript filter-seqs-length-by-taxon \
            --i-sequences {input.seqs} \
            --i-taxonomy {input.tax} \
            --p-labels {params.labels} \
            --p-min-lens {params.min_lens} \
            --o-filtered-seqs {output.filt_seqs} \
            --o-discarded-seqs {output.disc_seqs} \
            --verbose 2> {log.err} 1> {log.out}
        """

#####################################
# All Full-length SILVA Classifiers #
#####################################

rule all_full_train:
    wildcard_constraints:
        fl_type = ['fl-derep', 'filt', 'fl-cull']
    conda:
        Q2CONDA
    input:
         seqs = "fl/silva-{fltype}-seqs.qza",
         tax = rules.full_length_derep.output.derep_tax
    output:
        classifier = "fl/silva-{fltype}-classifier.qza"
    log:
        out = "logs/train-classifier-{fltype}-log.out",
        err = "logs/train-classifier-{fltype}-log.err"
    shell:
        """
	    qiime feature-classifier fit-classifier-naive-bayes \
            --i-reference-reads {input.seqs} \
            --i-reference-taxonomy {input.tax} \
            --o-classifier {output.classifier} \
            --verbose 2> {log.err} 1> {log.out}
	    """